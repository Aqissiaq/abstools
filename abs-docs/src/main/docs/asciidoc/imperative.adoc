
== Statements

This chapter specifies all ABS statements.

[caption=]
.Syntax
====
_Statement_ ::= _SkipStmt_
| _VarDeclStmt_
| _AssignStmt_
| _ExpStmt_
| _AssertStmt_
| _AwaitStmt_
| _SuspendStmt_
| _ThrowStmt_
| _ReturnStmt_
| _Block_
| _IfStmt_
| _CaseStmt_
| _WhileStmt_
| _TryCatchFinallyStmt_
====


=== Skip

The skip statement is a statement that does nothing.

[caption=]
.Syntax
====
_SkipStmt_ ::= `skip` `;`
====

[source]
.Example
----
skip;
----

=== Variable Declarations

A variable declaration statement is used to declare variables.  Variable
declarations can occur at any point in a sequence of statements; i.e., it is
not necessary to declare variables only at the beginning of methods or blocks.

Variables declared inside a block are in scope for the duration of the block.
It is an error to declare a variable with the same name of another variable in
scope.  A local variable can have the same name as an object field.

A variable declaration has an expression that defines the initial value of the
variable.  The initialization expression is mandatory except for variables of
reference types (interfaces and futures), in which case the variable is
initialized with `null` if the initialization expression is omitted.

[caption=]
.Syntax
====
_VarDeclStmt_ ::= _TypeName_ _Identifier_ [ `=` _Exp_ ] `;`
====

[source]
.Example
----
Bool b = True;
----


=== Variable Assignment

The assign statement assigns a value to a variable or a field.

Assignments to a field `f` can be written either `this.f = e;` or `f = e;`.
In case a local variable `f` is in scope at the point of the assignment
statement, the `this` prefix has to be used to assign to the field `f`; assignment
to `f` will change the value of the local variable.

[caption=]
.Syntax
====
_AssignStmt_ ::= [ `this` `.` ] _Identifier_ `=` Exp `;`
====

[source]
.Example
----
this.f = True;
x = 5;
----


=== Expressions as Statements

An expression statement is a statement that consists of a single expression.
When an expression statement is executed, the expression is evaluated and the
resulting value is discarded.

Expression statements are used for their side effects, for example issuing an
asynchronous method call without waiting for its result.

[caption=]
.Syntax
====
_ExpStmt_ ::= _Exp_ `;`
====

NOTE: Creating an object without storing a reference (and hence never invoking
a method on the new object) can be a meaningful operation, for example when
the object has a `run` method and interacts with the rest of the system by
calling methods on references passed in via the `new` expression.

[source]
.Example
----
server!operate();
new Client(server);
----


=== Assertions

An assert statement is a statement for asserting certain conditions.  If the
expression evaluates to `True`, executing an assertion is equivalent to
`skip;`.  If the expression evalutes to `False`, it is equivalent to `throw
AssertionFailException;`.

[caption=]
.Syntax
====
_AssertStmt_ ::= `assert` _PureExp_ `;`
====

[source]
.Example
----
assert x != null;
----



=== Process Release Point: Await

An await statement suspends the current task until the given guard becomes
active (evalutes to `True`).  While the task is suspended, other tasks within
the same COG can be scheduled.

In general, each cog will continue running a task without preempting it until
the task is finished or it reaches a _scheduling point_.  Await statements are
scheduling points, as are `suspend` statements and assignment or expression
statements containing an await expression (see <<Await Expression>>).

[caption=]
.Syntax
====
_AwaitStmt_ ::= `await` _Guard_ `;` +
_Guard_ ::=  [ `this` `.` ] _Identifier_ `?` +
| _PureExp_ +
| _Guard_ `&` _Guard_ +
| `duration` `(` _PureExp_ `,` _PureExp_ `)`
====

[source]
.Example
----
Fut<Bool> f = x!m();
await f?; <1>
await this.x == True; <2>
await f? & this.y > 5; <3>
await duration(3, 5); <4>
----
<1> A _claim guard_ becomes active when the future is resolved (contains a value).
<2> A _field guard_ is a Boolean expression over the object state.
<3> A _guard conjunction_ becomes active when both its components are active.
<4> A _duration guard_ becomes active after a certain amount of simulated time has passed.  See <<Real-Time ABS>> for more on timed models.



=== Unconditional Release: Suspend

The suspend statement causes the current task to be suspended.

[caption=]
.Syntax
====
_SuspendStmt_ ::= `suspend` `;`
====

NOTE: There is no guarantee that the cog will choose another task to run; the
current task might be resumed immediately after suspending itself.

[source]
.Example
----
suspend;
----



=== Return

A return statement returns a value from a method.  A return statement can only
appear as a last statement in a method body.

For asynchronous method calls, executing the return statement will cause the
future to be resolved so that it contains a value.  Any claim guards awaiting
the future will become active.

Methods that have a `Unit` return type do not need an explicit return
statement.  The future will be resolved when the method terminates.

[caption=]
.Syntax
====
_ReturnStmt_ ::= `return` _Exp_ `;`
====

NOTE: ABS does not allow exiting a method from multiple points, e.g., via
multiple `return` statements.  This makes model analysis easier.

[source]
.Example
----
return x;
----

=== Throw

The keyword-statement `throw` is used to signal exceptions (runtime errors).
It takes a single argument which is the exception-value to throw. For example:

[source]

----
{
 Int x = -1;
 if (x<0) {
    throw NegativeNumberException(x);
 }
 else {
   if (x==0) {
      throw ZeroNumberException;
   }
   else ...
}
----

NOTE: The 'throw' statement can only be used inside imperative code.
Throwing user-exceptions inside functional code is considered bad practice:
the user's function must be written instead to return an +Either<Exception, A>+ value,
as in the example:

----
def Either<Exception, Int> f(x,y) = if (y < 0)
                                    then Left(NegativeNumberException)
                                    else Right(...)
----

Despite this, there are certain built-in system-exceptions (see Section 3.3) that can originate
from erroneous functional code. Examples of these are
`DivisionByZeroException` and `PatternMatchFailException`, implicitly signaled
by the ABS system.

When an exception is raised (signaled), the normal flow of the program will be abrupted. In order to resume
the normal flow, the user has to explicitly *handle* the exception.



=== Blocks of Statements

A sequence of statements is called a _block_.  A block introduces a scope for
local variables.

[caption=]
.Syntax
====
_Block_ ::= `{` { _Statement_ } `}`
====

NOTE: Semantically, a whole block is a single statement and can be written
anywhere a single statement is valid.

[source]
.Example
----
{
  Int a = 0; <1>
  a = a + 1;
  n = a % 10;
}

{ } <2>
----
<1> The variable `a` is in scope until the end of the block.
<2> An empty block is equivalent to `skip;`.


=== Conditionals

[source]
.Example
----
if (5 < x) {
  y = 6;
}
else {
  y = 7;
}
if (True)
  x = 5;
----


=== Case: Pattern Matching

The case statement, like the case expression (see <<Case Expressions>>),
consists of an expression and a series of branches, each consisting of a
pattern and a statement (which can be a block).

When a case statement is executed, its input expression is evaluated and the
value matched against the branches until a matching pattern is found.  The
statement in the right-hand side of that branch is then executed.  Any
variable bindings introduced by matching the pattern are in effect while
executing that statement.

It is an error if no pattern matches the expression.

For a description of the patterns available, see the section on case
expressions (see <<Case Expressions>>).

[caption=]
.Syntax
====
_CaseStmt_ ::= `case` _PureExp_ `{` { _CaseStmtBranch_ } `}`
_CaseStmtBranch_ ::= _Pattern_ \=> _Stmt_
====

[source]
.Example
----
Pair<Int, Int> p = Pair(2, 3);
Int x = 0;
case p {
  Pair(2, y) => { x = y; skip; }
  _ => x = -1;
}
----



=== The While Loop

The while loop repeats its body while the condition evalutes to `True`.  The
condition is re-evaluated after each iteration of the loop.

[caption=]
.Syntax
====
_WhileStmt_ ::= `while` `(` _PureExp_ `)` _Stmt_
====

[source]
.Example
----
while (x < 5) {
  x = x + 1;
}
----



=== Handling Exceptions with Try-Catch-Finally

To handle an exception --- either explicitly signaled using the `throw` keyword or implicitly by a system exception ---
the user has to surround the offending code with a `try` block.
The statements in the try block will be executed in sequence until an exception happens.
Upon an exception, the execution of the try block will stop and the exception will be matched against the exception-patterns
defined in the `catch` block.

The catch block behaves similar to the `case statement`, with the only difference that the patterns
can only have the type `Exception`. When the exception-pattern is matched, the statements
associated with its catch clause will be executed. 

After defining the catch block, the user can _optionally_ supply a `finally` block
of statements, that will be executed regardless of an exception happening or not.

The syntax is the following:

[source]
.Example
----
try {
 stmt1;
 stmt2;
 ....
}
catch {
  exception_pattern1  => stmt_or_block;
  exception_pattern2  => ... ;
  ...
  _ => ...
}
finally {
  stmt3;
  stmt4;
}
----

If there are no matching catch-clauses,
the finally block will first be accordingly executed,
before re-throwing the exception to its parent caller.
Conversely, if the parent caller does not (correctly) handle the re-thrown exception,
the exception will be propagated to its own parent caller, and so forth and so on.
