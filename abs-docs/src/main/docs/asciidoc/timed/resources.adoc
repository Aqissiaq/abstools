== Resource Modeling in ABS

Resource Modeling deals with simulating and analyzing the non-functional
properties of models: code deployment on varying numbers and kinds of
machines, and the effects of different CPU speeds, interconnection bandwidth,
code locality, etc. on the performance of a system.  This section describes
the constructs ABS offers to the modeler.

All language identifiers described in this section reside in the `ABS.DC` package.  To use them, define a module as follows:

----
module Name;
import * from ABS.DC;
----

=== Resources

The term _Resource_ can be used in different ways.  In ABS, we understand a
Resource to be a countable, measurable property that is influenced by program
execution and the passage of time.  The resources that are supported by the ABS language and its simulation and analysis tools are the following:

CPU :: A measure of execution speed, applies to any ABS statement
Bandwidth :: A measure of transmission speed, applies to method invocation and
`return` statements
Memory :: A measure of cog size, applies to class definitions

These types of resource are defined in the `ABS.DC` module as follows:

----
data Resourcetype = CPU | Bandwidth | Memory ;
----

=== Deployment Components

Modeling code deployment and code execution under resource constraints
requires a notion of _locality_.  For this purpose, ABS offers a language
construct called Deployment Component.

Deployment Components are first-class language constructs in that they can be
created, referenced and interacted with from within the model.  A reference to
a deployment component is treated the same way as a reference to an object.
Deployment Components are created using the `new` expression.  Any other cog
can be created "on" a deployment component by using a `DC` annotation to the
`new` statement.

.Resources and Deployment Components

Each deployment component "carries" some possibly infinite amount of resources
for each resource type.  This is expressed as a map from resource type to a
number, for example `map[Pair(CPU, 10), Pair(Bandwidth, 20)]`.  Resource types
not included, such as `Memory` in the above example, are treated as being
infinite.


.Example
----
DeploymentComponent dc = new DeploymentComponent("Server 1", map[Pair(CPU, 10)]); <1>
[DC: dc] Worker w = new CWorker(); <2>
----
<1> A new deployment component `dc` is created with 10 `CPU` resources
<2> `w` will run inside `dc`

NOTE: It is an error to try to locally create deployment components (via `new
local DeploymentComponent(...)`) or new local objects on another cog (via
`[DC: x] new local C()`).


=== Modeling Resource Usage

Resource models are added to an ABS model using annotations.  Adding annotations to specific statements and declarations causes side-effects on the status of an applicable deployment component, as described in this section.

.Example
----
module Test;
import * from ABS.DC; <1>
interface I {
  Unit process();
}
[Size: 3] <2>
class C implements I {
  Unit process() {
    [Cost: 10] skip; <3>
}

{
  DeploymentComponent dc = new DeploymentComponent("Server",
    map[Pair(CPU, 5), Pair(Bandwidth, 10), Pair(Memory, 5)]);
  [DC: dc] I i = new C(); <4>
  [Size: 5] i!process(); <5>
}
----
<1> Make all necessary identifiers accessible in the current module
<2> Declare the memory needed to instantiate a cog of class `C`
<3> Executing this statement costs 10 CPU units; the time needed depends on the CPU capacity of the deployment component, and on other cogs executing in parallel on the same deployment component.
<4> Creating a new cog succeeds since the available memory (5) is more than the necessary memory (3).  Trying to create a second cog will fail.
<5> Executing this method call consumes 5 Bandwidth resources.

