
/*
 * PingPong.abs -- PingPong example
 *
 * An  ABS program. To test core parser.  
 *
 */

data Msg = NoMsg 
         | HelloPing 
         | HelloPong(Ping)
         | ThanksPong
         | ThanksPing
         | HowAreYou
         | Fine
         | Bye 
         ;


interface Ping {
    Unit m(Msg m);
    Unit run()	;
}

interface Pong {
    Unit hello(Ping ping);
    Unit m(Msg m);
}

class PingImpl(Pong pong) implements Ping {

    Unit run(){
      pong!hello(this); 
    }

    Unit m(Msg msg){
      Msg reply = case msg {
         HelloPing => HowAreYou;
         Fine => Bye; 
         Bye => NoMsg;
      };
      
      if (reply != NoMsg)
         pong!m(reply);
     }
} 

class PongImpl implements Pong {
    Ping ping;

    Unit hello(Ping ping) {
       this.ping = ping;
       ping!m(HelloPing);
    }

    Unit m(Msg msg){
       if (msg == HowAreYou)
           ping!m(Fine);
       else
           ping!m(Bye);
    } 

}

{
    Ping ping;
    Pong pong;
     
    pong = new cog PongImpl(); 
    ping = new cog PingImpl(pong); 

    ping!run(); 
}
