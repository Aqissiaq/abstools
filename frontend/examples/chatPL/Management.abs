module Management;
export *;
import * from ABS.Meta;


data Bandwidth = Low | Mid | High;

interface Connection {
    Bandwidth checkBandwidth();
}


class ConnectionImpl implements Connection {
    Bandwidth checkBandwidth() {
        // randomly return a value
        Int r = random(3);
        return case r {
	        0 => Low;
	        1 => Mid;
	        2 => High;
        };
    }
}

class Reconfigurator(Connection conn) {
    Unit run() {
        Runtime rt = new local Runtime();
        ProductLine pl = rt.getProductLine();

        while(True) {
            Product p = pl.getCurrentProduct();
            Bandwidth bw = conn.checkBandwidth();
            String name = p.getName();
            if (name == "RegularChat") {
                if (bw == Low) {
                    Product p2 = pl.getConfigurableProduct("LowEndChat");
                    pl.configureProduct(p2);
                } else if (bw == High) {
                    Product p2 = pl.getConfigurableProduct("HighEndChat");
                    pl.configureProduct(p2);
                }
            } else if (name == "HighEndChat") {
                if (bw == Low || bw == Mid) {
                    Product p2 = pl.getConfigurableProduct("RegularChat");
                    pl.configureProduct(p2);
                }
            } else if (name == "LowEndChat") {
                if (bw == Mid || bw == High) {
                    Product p2 = pl.getConfigurableProduct("RegularChat");
                    pl.configureProduct(p2);
                }
            }
        }
    }
}

