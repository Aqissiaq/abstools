module Management;
export *;
import * from ABS.Meta;


data Bandwidth = Low | Mid | High;

interface Connection {
    Bandwidth checkBandwidth();
}


class ConnectionImpl implements Connection {
    Bandwidth checkBandwidth() {
        // randomly return a value
        Int r = random(3);
        return case r {
	        0 => Low;
	        1 => Mid;
	        2 => High;
        };
    }
}

class Reconfigurator(Connection conn) {
    Unit run() {
        ProductLine pl = getProductLine();

        while(True) {
            Product p = pl.getCurrentProduct();
            Bandwidth bw = conn.checkBandwidth();
            String name = p.getName();
            if (name == "RegularChat") {
                if (bw == Low) {
                    pl.configureProduct("LowEndChat");
                } else if (bw == High) {
                    pl.configureProduct("HighEndChat");
                }
            } else if (name == "HighEndChat") {
                if (bw == Low || bw == Mid) {
                    pl.configureProduct("RegularChat");
                }
            } else if (name == "LowEndChat") {
                if (bw == Mid || bw == High) {
                    pl.configureProduct("RegularChat");
                }
            }
        }
    }
}

