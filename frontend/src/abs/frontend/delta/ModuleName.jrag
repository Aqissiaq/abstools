aspect ModuleName {

    //inform every Decl about the name of the Module where it was declared
    inh String Decl.moduleName();
    eq ModuleDecl.getDecl().moduleName() = this.getName();

    inh String ClassModifier.moduleName();
    eq DeltaDecl.getClassModifier().moduleName() = this.moduleName();
    
    inh String Product.moduleName();
    eq ModuleDecl.getProduct().moduleName() = this.getName();

    inh String ProductLine.moduleName();
    eq ModuleDecl.getProductLine().moduleName() = this.getName();

    inh String DeltaClause.moduleName();
    eq ProductLine.getDeltaClause().moduleName() = this.moduleName();
}


aspect QualifiedName {
    
    // prepend the name of the module where this declaration occured
    public String Decl.qualifiedName() {
        return this.moduleName() + "." + this.getName();
    }

    public String Product.qualifiedName() {
        return this.moduleName() + "." + this.getName();
    }

    public String ProductLine.qualifiedName() {
        return this.moduleName() + "." + this.getName();
    }
}