//$Id$ 
//
 

Model                         ::= CompilationUnit*;
 
CompilationUnit ::= <Name> ModuleDecl* ;
ModuleDecl ::= Name:Name Export* Import* Decl* [Block:MainBlock] ;

abstract Import;
NamedImport : Import ::= Name* ;
StarImport : Import ::= ModuleName:Name;
FromImport : Import ::= SimpleName* ModuleName:Name;

abstract Export;
NamedExport: Export ::= Name* ;
StarExport: Export;
// (see below for Blocks)

abstract 				Decl ::= <Name>;
abstract BehaviorDecl:	Decl ::= ; // Entities used in behavior expressions
abstract TypeDecl:		Decl; // Entities used in type expressions

UnknownDecl:	TypeDecl;		// Unknown declarations
ClassDecl:		BehaviorDecl	::=  Annotation* Param:ParamDecl* ImplementedInterfaceUse:InterfaceTypeUse* [InitBlock] Field:FieldDecl* Method:MethodImpl*;
FunctionDecl:	BehaviorDecl    ::=  Annotation* TypeUse Param:ParamDecl* FunctionDef ;
ParametricFunctionDecl : FunctionDecl ::=  Annotation* TypeParameter:TypeParameterDecl* ;
InterfaceDecl:	TypeDecl		::=  Annotation* ExtendedInterfaceUse:InterfaceTypeUse* Body:MethodSig*;
DataTypeDecl:	TypeDecl		::=  Annotation* DataConstructor* ;
TypeParameterDecl	: TypeDecl 	::= <Name>;  // local declaration
ParametricDataTypeDecl : DataTypeDecl ::=  Annotation* TypeParameter:TypeParameterDecl* ;
TypeSynDecl:    TypeDecl        ::=  Annotation* Value:DataTypeUse ; 

abstract Name;
QualifiedName : Name ::= SimpleName*;
SimpleName : Name ::= <Name>;

abstract	VarOrFieldDecl  ::= <Name>;
abstract TypedVarOrFieldDecl : VarOrFieldDecl ::= Access; 
VarDecl:	TypedVarOrFieldDecl  ::= [InitExp:Exp];
FieldDecl:	TypedVarOrFieldDecl ::= [InitExp:PureExp];
ParamDecl:	TypedVarOrFieldDecl;
PatternVarDecl: VarOrFieldDecl;

abstract FunctionDef;
BuiltinFunctionDef: FunctionDef;
ExpFunctionDef: FunctionDef ::= Rhs:PureExp;

MethodSig	::= <Name> Annotation* ReturnType:Access Param:ParamDecl*;
MethodImpl  ::= MethodSig Block;


// Uses of various identifiers
abstract Access:		PureExp;
abstract IdUse:			Access	::= Name:Name;
abstract TypeUse:		IdUse;
InterfaceTypeUse:		TypeUse; // TODO: reconsider hierarchy of expressions?
DataTypeUse:			TypeUse;
ParametricDataTypeUse:  DataTypeUse ::= Param:DataTypeUse* ; // FIXME: should be TypeParameterUse - fix in ABS.parser as well
TypeParameterUse:		TypeUse;
abstract VarOrFieldUse: Access ::= <Name>;
VarUse:					VarOrFieldUse;
FieldUse:				VarOrFieldUse;        // this.f

UnresolvedTypeUse:		TypeUse;	// A type expression whose type cannot be resolved at parse time.
									// Will be  resolved by rewriting.   TODO: needed? 


/*
Constructors occur as 
- DataConstructors as in data declarations Co(_D_)
- term consctructors as in Co(_t_)
- patterns as in Co(_p_)
- Don't know yet how to organise these.   
*/

abstract Constructor ::= <Name>; 
DataConstructor : Constructor ::= ConstructorArg:DataTypeUse* ;

Annotation      ::= <Name> Value:PureExp;

abstract Stmt   ::= Annotation* ; 
abstract InitStmt:		Stmt;   //FIXME not all statements can be init statements, i.e. occur in an init block

AssignStmt: Stmt		::= Var:PureExp Value:Exp;
AwaitStmt: Stmt			::= Guard;
SkipStmt: Stmt;
SuspendStmt: Stmt;
ReturnStmt: Stmt		::= RetExp:Exp; 
ExpressionStmt: Stmt	::= Exp:Exp;
AssertStmt: Stmt        ::= Condition:PureExp;
VarDeclStmt: Stmt ::= VarDecl:VarDecl;

IfStmt: Stmt			::= Condition:PureExp Then:Stmt [Else:Stmt];
WhileStmt: Stmt			::= Condition:PureExp Body:Stmt;
// A Block is the body of programs and methods and is also used for initblocks
// It contains a list of zero or more ast.VarOrFieldDecl 
// and zero or more ast.Stmt, these are named Fields and Stmts resp.  
Block: Stmt				::= Stmt:Stmt*;
InitBlock: Block;		// Initializer block in a class - can be handled specially during code generation
MainBlock: Block;             // Active behavior of the model


abstract Guard;
ClaimGuard: Guard		::= Var:PureExp;
AndGuard: Guard			::= Left:Guard Right:Guard;
//For boolean guards   
ExpGuard: Guard			::= PureExp ; 


// Expressions 
abstract Exp;
// Pure expressions
abstract PureExp: Exp;
// Expressions with side effect. 
abstract EffExp: Exp;

// Side effect free expressions 
NullExp: PureExp;
ThisExp: PureExp;
// plus *Use-s

abstract LiteralExp: PureExp;
// For now the literal just contain a variable Content of type String. 
StringLiteral: LiteralExp  ::= <Content> ;
IntLiteral: LiteralExp ::= <Content>;        

DataConstructorExp : PureExp  ::= Constructor:Name Param:PureExp* ;
LetExp  : PureExp ::= Var:ParamDecl Val:PureExp Exp:PureExp ; 
FnApp  : PureExp ::= Name:Name Param:PureExp* ;
CaseExp  : PureExp ::= Expr:PureExp Branch:CaseBranch* ;
CaseBranch ::= Left:Pattern Right:PureExp ; 
UnresolvedExp : PureExp ::= Name:Name;

//Patterns 
abstract Pattern ; 

PatternVarUse:    Pattern ::= <Name>;
PatternVar:			Pattern ::= Var:PatternVarDecl; 
ConstructorPattern: Pattern ::= Constructor:Name Param:Pattern*;
LiteralPattern:		Pattern ::= Literal:LiteralExp;
//PatternPair:		Pattern ::= Left:Pattern Right:Pattern;
UnderscorePattern:	Pattern; 
UnresolvedPattern:  Pattern ::= Name;


// Expressions with side effects. 
NewExp: EffExp			::= ClassName:Name Param:PureExp* <Cog> ; //todo, optional cog 
abstract Call: EffExp 	::= Callee:PureExp <Method> Param:PureExp*;
SyncCall: Call;
AsyncCall: Call;
GetExp: EffExp			::= PureExp;


abstract Unary : PureExp ::= Operand:PureExp;

abstract Binary : PureExp ::= Left:PureExp Right:PureExp; 


//Integers
MinusExp : Unary ;  

abstract ArithmeticExp : Binary ; 

abstract MultExp :  ArithmeticExp ;   
MultMultExp  : MultExp ;
DivMultExp  : MultExp ;
ModMultExp : MultExp ;

abstract AddExp :  ArithmeticExp ;
AddAddExp  : AddExp ;
SubAddExp : AddExp ;

//Booleans 
NegExp: Unary ; 

abstract BoolExp : Binary ; 
AndBoolExp : BoolExp ; 
OrBoolExp : BoolExp ; 

//Relations
abstract RelationalExpr : Binary;
LTExp : RelationalExpr ;
GTExp : RelationalExpr ;
LTEQExp : RelationalExpr ;
GTEQExp : RelationalExpr ;

abstract EqualityExpr : RelationalExpr;
EqExp : EqualityExpr ;
NotEqExp : EqualityExpr ;


/*
// Created by NTA equations
InterfaceRef:	TypeAccess;
PrimitiveTypeRef: TypeAccess;	// e.g. bool
UnknownDecl:	TypeAccess;
*/